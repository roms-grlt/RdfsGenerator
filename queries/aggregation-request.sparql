# Requête d'agrégation multi-sources : statistiques par genre avec raisonnement RDFS
# Utilise subPropertyOf : imdb:genre/netflix:genre -> unified:genre
# Agrège les ratings depuis Amazon et Netflix via unified:rating
PREFIX unified: <http://example.org/unified/>
PREFIX amazon: <http://example.org/amazon/>
PREFIX netflix: <http://example.org/netflix/>
PREFIX imdb: <http://example.org/imdb/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?genre
       (COUNT(DISTINCT ?film) as ?totalFilms)
       (ROUND(AVG(?duration)) as ?avgDuration)
       (ROUND(AVG(?rating)) as ?avgRating)
       (MIN(?rating) as ?minRating)
       (MAX(?rating) as ?maxRating)
WHERE {
  # Récupérer les films avec genres, rating et duration (via raisonnement RDFS)
  ?film a unified:film .

  ?film unified:genre ?genre .
  ?film unified:rating ?rating .
  ?film unified:duration ?duration .
}
GROUP BY ?genre
HAVING (COUNT(DISTINCT ?film) > 5)
ORDER BY DESC(?totalFilms)