PREFIX amazon: <http://example.org/amazon/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?localTitle ?rating
       (GROUP_CONCAT(DISTINCT ?country; separator=", ") AS ?countries) 
       ?year
WHERE {
  ?film a amazon:AmazonFilm ;
        amazon:title ?localTitle ;
        amazon:rating ?rating ;

  SERVICE <https://query.wikidata.org/sparql> {
    {
      SELECT ?wikidataFilm ?wikidataTitle ?country (YEAR(MIN(?date)) AS ?year)
      WHERE {
        ?wikidataFilm wdt:P31 wd:Q11424 ;
                      wdt:P57 wd:Q25191 ;
                      rdfs:label ?wikidataTitle ;
                      wdt:P495 ?countryEntity ;
                      wdt:P577 ?date .

        ?countryEntity rdfs:label ?country .

        FILTER(LANG(?wikidataTitle) = "en")
        FILTER(LANG(?country) = "en")
      }
      GROUP BY ?wikidataFilm ?wikidataTitle ?country
    }
  }

  # Matcher les titres locaux avec les titres Wikidata (insensible Ã  la casse)
  FILTER(LCASE(STR(?wikidataTitle)) = LCASE(STR(?localTitle)))
}
GROUP BY ?localTitle ?rating ?year
ORDER BY ?year