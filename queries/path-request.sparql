# Requete avec expressions de chemin RDFS (subPropertyOf*)
PREFIX unified: <http://example.org/unified/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?title ?ratingValue ?ratingProp ?genreValue ?genreProp
WHERE {
  ?film a unified:film .

  # Utilise subPropertyOf* pour trouver tous les titres
  ?titleProp rdfs:subPropertyOf* unified:title .
  ?film ?titleProp ?title .

  # Utilise subPropertyOf* pour trouver tous les ratings
  OPTIONAL {
    ?ratingProp rdfs:subPropertyOf* unified:rating .
    ?film ?ratingProp ?ratingValue .
  }

  # Utilise subPropertyOf* pour trouver tous les genres
  OPTIONAL {
    ?genreProp rdfs:subPropertyOf* unified:genre .
    ?film ?genreProp ?genreValue .
  }
}
ORDER BY ?title
LIMIT 30
